//Standard Installation

/*
Variables and journal entry for IWDEE/IWD1
*/


ACTION_IF (GAME_IS ~iwdee iwd how totlm iwd_in_bg2~) BEGIN


//Set Global variables (check if party has items)
<<<<<<<< .../temp/variables
IF
	PartyHasItem("%bigmetalrod%")
	!Global("d2HasBigMetalRod","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasBigMetalRod","GLOBAL",1)
		Continue()
END

IF
	PartyHasItem("%pulse%")
	!Global("d2HasPulseAmmo","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasPulseAmmo","GLOBAL",1)
		Continue()
END

IF
	PartyHasItem("%silver%")
	!Global("d2HasSilverPants","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasSilverPants","GLOBAL",1)
		Continue()
END

IF
	PartyHasItem("%bronze%")
	!Global("d2HasBronzePants","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasBronzePants","GLOBAL",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/variables~  ~inlined/variables.BAF~
  EVALUATE_BUFFER

ACTION_FOR_EACH script IN ~AR2100~ ~AR9100~ BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%script%.BCS~) BEGIN
    EXTEND_TOP ~%script%.BCS~ ~inlined/variables.BAF~
  END
END



//Journal entry (got the rod)
<<<<<<<< .../temp/pomabrod
IF
	!PartyHasItem("%bigmetalrod%")
	Global("d2PomabBigMetal","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("d2PomabBigMetal","GLOBAL",1)
		Continue()
END

IF
	PartyHasItem("%bigmetalrod%")
	Global("AR1016_visited","GLOBAL",1)
	Global("d2PomabBigMetal","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2PomabBigMetal","GLOBAL",2)
		AddJournalEntry(%iwdjour01%)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/pomabrod~  ~inlined/pomabrod.BAF~
  EVALUATE_BUFFER
EXTEND_TOP ~AR1000.BCS~  ~inlined/pomabrod.BAF~


END


/*
*/