/*
Dialogues for discussing and/or trading for items.
The ones with (mira = 1) are only added with Standard install.
*/


//prep

//iwd1 (check dlg states)
OUTER_TEXT_SPRINT orrick_states  ~0 2 18 19~               // DORRICK
OUTER_TEXT_SPRINT oswald_states  ~12 14~                   // DOSWALD
OUTER_TEXT_SPRINT edion_states   ~9 10 11 12 16~           // DEDION
OUTER_TEXT_SPRINT tiernon_states ~16 17 26 27 28 33 46~    // DTIERNON

ACTION_IF (GAME_IS ~iwdee~) BEGIN                                   // if iwdee
  OUTER_TEXT_SPRINT orrick_states  ~%orrick_states% 23 28 38~
  OUTER_TEXT_SPRINT oswald_states  ~%oswald_states% 31 33~
  OUTER_TEXT_SPRINT edion_states   ~%edion_states% 29~
  OUTER_TEXT_SPRINT tiernon_states ~%tiernon_states% 78~
END
ELSE BEGIN
  ACTION_IF (MOD_IS_INSTALLED ~setup-iu_iwd.tp2~ 0) BEGIN           // if classic iwd with 'item upgrade'
    OUTER_TEXT_SPRINT orrick_states  ~%orrick_states% 29~
    OUTER_TEXT_SPRINT oswald_states  ~%oswald_states% 31 33~
    OUTER_TEXT_SPRINT edion_states   ~%edion_states% 29~
    OUTER_TEXT_SPRINT tiernon_states ~%tiernon_states% 78~
    ACTION_IF (RESOURCE_CONTAINS ~DORRICK.DLG~ ~beetshld~) BEGIN    // if UB mod
      OUTER_TEXT_SPRINT orrick_states  ~%orrick_states% 33 38~ 
    END
  END
  ELSE BEGIN
    ACTION_IF (RESOURCE_CONTAINS ~DORRICK.DLG~ ~beetshld~) BEGIN    // if UB with no 'item upgrade'
      OUTER_TEXT_SPRINT orrick_states  ~%orrick_states% 23 28~
    END
  END
END


//iwd2 (fix weidu warning)
ACTION_IF (FILE_EXISTS_IN_GAME ~11OSWALD.DLG~) BEGIN
  COPY_EXISTING ~11OSWALD.DLG~  ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY EXACT_MATCH  ~StartCutscene(#53cKulda#)~ ~StartCutscene("53cKulda")~
    END
  BUT_ONLY
END


//check DestroyGold script action (not in BG1/IWD1)
ACTION_IF (GAME_IS ~iwd how totlm bg1 totsc~) BEGIN
  OUTER_TEXT_SPRINT destroygold ~//~
END ELSE BEGIN
  OUTER_TEXT_SPRINT destroygold ~DestroyGold~
END


//check BG1 script actions (black lily, diviner)
ACTION_IF (GAME_IS ~bg1 totsc~) BEGIN

OUTER_TEXT_SPRINT lilytalk
~~~~~
IF ~ PartyHasItem("%metalrodb%")    // has Big Metal Rod
     Global("d2BlackLilyBigMetal","GLOBAL",0)
   ~ GOTO BlackLilyOffer

IF ~ PartyHasItem("%bigmetalrod%")    // bg2 text
     Global("d2BlackLilyBigMetal","GLOBAL",0)
   ~ GOTO BlackLilyOffer
~~~~~

END ELSE BEGIN

OUTER_TEXT_SPRINT lilytalk
~~~~~
IF ~ OR(2)
       PartyHasItem("%metalrodb%")    // has Big Metal Rod
       PartyHasItem("%bigmetalrod%")
     Global("d2BlackLilyBigMetal","GLOBAL",0)
   ~ GOTO BlackLilyOffer
~~~~~

END



//
//add to DLGs
ACTION_IF (GAME_IS ~iwdee iwd how totlm iwd_in_bg2~) BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~DOSWALD.DLG~)  BEGIN COMPILE EVAL ~%dfiles%/iwd1_oswald.d~ END
  ACTION_IF (FILE_EXISTS_IN_GAME ~DHOBART.DLG~)  BEGIN COMPILE EVAL ~%dfiles%/iwd1_hobart.d~ END
  ACTION_IF (FILE_EXISTS_IN_GAME ~DEDION.DLG~)   BEGIN COMPILE EVAL ~%dfiles%/iwd1_edion.d~ END

  ACTION_IF (mira = 1) BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~DORRICK.DLG~)  BEGIN COMPILE EVAL ~%dfiles%/iwd1_orrick.d~ END
    ACTION_IF (FILE_EXISTS_IN_GAME ~DTIERNON.DLG~) BEGIN COMPILE EVAL ~%dfiles%/iwd1_tiernon.d~ END
    ACTION_IF (FILE_EXISTS_IN_GAME ~DEMMRCH.DLG~)  BEGIN COMPILE EVAL ~%dfiles%/iwd1_emmerich.d~ END
  END

END ELSE ACTION_IF (GAME_IS ~iwd2~) BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~11OSWALD.DLG~) BEGIN COMPILE EVAL ~%dfiles%/iwd2_oswald.d~ END
  ACTION_IF (FILE_EXISTS_IN_GAME ~40BDAWN.DLG~)  BEGIN COMPILE EVAL ~%dfiles%/iwd2_beodaewn.d~ END
  ACTION_IF (FILE_EXISTS_IN_GAME ~41ZACK.DLG~)   BEGIN COMPILE EVAL ~%dfiles%/iwd2_zack.d~ END
  ACTION_IF (FILE_EXISTS_IN_GAME ~20RIKI.DLG~)   BEGIN COMPILE EVAL ~%dfiles%/iwd2_riki.d~ END
  ACTION_IF (FILE_EXISTS_IN_GAME ~60NATHA2.DLG~) BEGIN COMPILE EVAL ~%dfiles%/iwd2_nathaniel.d~ END

  ACTION_IF (mira = 1) BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~50NYM.DLG~)    BEGIN COMPILE EVAL ~%dfiles%/iwd2_nym.d~ END
    ACTION_IF (FILE_EXISTS_IN_GAME ~52BERED.DLG~)  BEGIN COMPILE EVAL ~%dfiles%/iwd2_bered.d~ END
    ACTION_IF (FILE_EXISTS_IN_GAME ~60CONLA2.DLG~) BEGIN COMPILE EVAL ~%dfiles%/iwd2_conlan.d~ END
    ACTION_IF (FILE_EXISTS_IN_GAME ~11OSWALD.DLG~) BEGIN COMPILE EVAL ~%dfiles%/iwd2_oswald2.d~ END
  END

END ELSE BEGIN    // bg games

  ACTION_IF (FILE_EXISTS_IN_GAME ~BLACKL.DLG~)   BEGIN COMPILE EVAL ~%dfiles%/bg1_blacklily.d~ END
  ACTION_IF (FILE_EXISTS_IN_GAME ~DIVINE.DLG~)   BEGIN COMPILE EVAL ~%dfiles%/bg1_diviner.d~ END
  ACTION_IF (FILE_EXISTS_IN_GAME ~LUCKY.DLG~)    BEGIN COMPILE EVAL ~%dfiles%/bg1_lucky.d~ END
  ACTION_IF (FILE_EXISTS_IN_GAME ~BDHALBAZ.DLG~) BEGIN COMPILE EVAL ~%dfiles%/sod_halbaz.d~ END
  ACTION_IF (FILE_EXISTS_IN_GAME ~RIBALD.DLG~)   BEGIN COMPILE EVAL ~%dfiles%/bg2_ribald.d~ END
  ACTION_IF (FILE_EXISTS_IN_GAME ~BOTSMITH.DLG~) BEGIN COMPILE EVAL ~%dfiles%/bg2_cespenar.d~ END

  ACTION_IF (mira = 1) BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~LEHTIN.DLG~)   BEGIN COMPILE EVAL ~%dfiles%/bg2_lehtinan.d~ END
    ACTION_IF (FILE_EXISTS_IN_GAME ~HENDAK.DLG~)   BEGIN COMPILE EVAL ~%dfiles%/bg2_hendak.d~ END
  END

  // extra patching for Lucky Aello
  COPY_EXISTING ~LUCKY.dlg~  ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY EXACT_MATCH ~True()~ ~~    // remove true trigger if not already removed
    END
  IF_EXISTS BUT_ONLY

END


/*
*/