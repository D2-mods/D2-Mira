/*
update charges for DLG or BCS files
*/


//array with default lines
ACTION_DEFINE_ASSOCIATIVE_ARRAY d2mira_charges BEGIN
//bg1/sod
~BLACKL.DLG~    => ~GiveItemCreate("%prototypeb%",LastTalkedToBy,0,0,0)~
~BDHALBAZ.DLG~  => ~GiveItemCreate("%prototypeb%",LastTalkedToBy,0,0,0)~
//~bd3000.BCS~    => ~AddStoreItem("BDBELEG4","%prototype%",1,1)~
~bd3000.BCS~    => ~ActionOverride("PlayerChest00",CreateItem("%prototype%",0,0,0))~

//bg2/tob
~RIBALD.DLG~    => ~GiveItemCreate("%prototype%",LastTalkedToBy,0,0,0)~
~BOTSMITH.BCS~  => ~GiveItemCreate("%pulseIII%",Player1,0,0,0)~

//iwd1/how
~DOSWALD.DLG~   => ~GiveItemCreate("%prototype%",LastTalkedToBy,0,0,0)~
~DORRICK.DLG~   => ~GiveItemCreate("%pulse%",LastTalkedToBy,0,0,0)~
~DEMMRCH.DLG~   => ~GiveItemCreate("%pulse%",LastTalkedToBy,0,0,0)~
~DHOBART.DLG~   => ~GiveItemCreate("%pulseIII%",LastTalkedToBy,0,0,0)~

//iwd2
~40BDAWN.DLG~   => ~GiveItemCreate("%prototype%",LastTalkedToBy,0,0,0)~
~41ZACK.DLG~    => ~GiveItemCreate("%prototype%",LastTalkedToBy,0,0,0)~
~50NYM.DLG~     => ~GiveItemCreate("%pulse%",LastTalkedToBy,0,0,0)~
~52BERED.DLG~   => ~GiveItemCreate("%pulse%",LastTalkedToBy,0,0,0)~
~11OSWALD.DLG~  => ~GiveItemCreate("%pulse%",LastTalkedToBy,0,0,0)~
~20RIKI.DLG~    => ~GiveItemCreate("%pulseIII%",LastTalkedToBy,0,0,0)~
END


//array with patched lines
ACTION_DEFINE_ASSOCIATIVE_ARRAY d2mira_charges_patched BEGIN
//bg1/sod
~BLACKL.DLG~    => ~GiveItemCreate("%prototypeb%",LastTalkedToBy,%charges%,0,0)~
~BDHALBAZ.DLG~  => ~GiveItemCreate("%prototypeb%",LastTalkedToBy,%charges%,0,0)~
//~bd3000.BCS~    => ~AddStoreItem("BDBELEG4","%prototype%",%charges%,1)~
~bd3000.BCS~    => ~ActionOverride("PlayerChest00",CreateItem("%prototype%",%charges%,0,0))~

//bg2/tob
~RIBALD.DLG~    => ~GiveItemCreate("%prototype%",LastTalkedToBy,%charges%,0,0)~
~BOTSMITH.BCS~  => ~GiveItemCreate("%pulseIII%",Player1,%charges%,0,0)~

//iwd1/how
~DOSWALD.DLG~   => ~GiveItemCreate("%prototype%",LastTalkedToBy,%charges%,0,0)~
~DORRICK.DLG~   => ~GiveItemCreate("%pulse%",LastTalkedToBy,%charges%,0,0)~
~DEMMRCH.DLG~   => ~GiveItemCreate("%pulse%",LastTalkedToBy,%charges%,0,0)~
~DHOBART.DLG~   => ~GiveItemCreate("%pulseIII%",LastTalkedToBy,%charges%,0,0)~

//iwd2
~40BDAWN.DLG~   => ~GiveItemCreate("%prototype%",LastTalkedToBy,%charges%,0,0)~
~41ZACK.DLG~    => ~GiveItemCreate("%prototype%",LastTalkedToBy,%charges%,0,0)~
~50NYM.DLG~     => ~GiveItemCreate("%pulse%",LastTalkedToBy,%charges%,0,0)~
~52BERED.DLG~   => ~GiveItemCreate("%pulse%",LastTalkedToBy,%charges%,0,0)~
~11OSWALD.DLG~  => ~GiveItemCreate("%pulse%",LastTalkedToBy,%charges%,0,0)~
~20RIKI.DLG~    => ~GiveItemCreate("%pulseIII%",LastTalkedToBy,%charges%,0,0)~
END



//iwd2ee: fix weidu warning (20riki)
ACTION_IF (FILE_EXISTS ~IEex.dll~ AND FILE_EXISTS ~IWD2EE.exe~) BEGIN
  COPY_EXISTING ~20riki.dlg~  ~override~
    DECOMPILE_AND_PATCH BEGIN
      TEXT_SPRINT match  ~^.*35916.*GOTO 35.*$~
      TEXT_SPRINT string "~ THEN REPLY #35916 GOTO 35"
      REPLACE_TEXTUALLY ~%match%~  ~%string%~
    END
  IF_EXISTS BUT_ONLY
END

//iwd-in-bg2
ACTION_IF (FILE_CONTAINS_EVALUATED (~weidu.log~  ~ICE_TUTU_TWEAKS~)) BEGIN
  COPY_EXISTING ~DORRICK.dlg~  ~override~
    DECOMPILE_AND_PATCH BEGIN
      TEXT_SPRINT match   ~AddXPVar("Level_7.*)~
      TEXT_SPRINT string  ~AddXP2DA("Level_7_Average")~
      REPLACE_TEXTUALLY ~%match%~  ~%string%~
    END
  IF_EXISTS BUT_ONLY
END


//
//patch DLGs
SILENT
ACTION_PHP_EACH d2mira_charges AS file_to_patch => old_line BEGIN
  ACTION_IF (FILE_EXISTS ~override/%file_to_patch%~) BEGIN
    COPY_EXISTING ~%file_to_patch%~  ~override~
      DECOMPILE_AND_PATCH BEGIN
        PHP_EACH d2mira_charges_patched AS match_file => new_line BEGIN
          PATCH_IF (~%file_to_patch%~ STR_EQ ~%match_file%~) BEGIN
            REPLACE_TEXTUALLY EXACT_MATCH ~%old_line%~  ~%new_line%~
          END
        END
      END
    BUT_ONLY
  END
END
VERBOSE

//the SILENT is to not interrupt PI installs if compiler notices issues with DLG (caused by other mods)
//may show WARNING message after install, but it's harmless for this mod


//
//BG2 patch Amkethran smith
ACTION_IF (FILE_EXISTS_IN_GAME ~AMSMITH.DLG~) BEGIN
  COPY_EXISTING ~AMSMITH.DLG~  ~override~
    DECOMPILE_AND_PATCH BEGIN
      PATCH_FOR_EACH ammo_type IN ~secret02~ ~secret03~ ~secret04~ BEGIN
         TEXT_SPRINT old_line ~GiveItemCreate("%ammo_type%",LastTalkedToBy(),0,0,0)~
         TEXT_SPRINT new_line ~GiveItemCreate("%ammo_type%",LastTalkedToBy(),%charges%,0,0)~
         REPLACE_TEXTUALLY EXACT_MATCH ~%old_line%~ ~%new_line%~
      END
    END
  BUT_ONLY
END


/*
*/