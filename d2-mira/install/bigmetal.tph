/*
install items
*/


//
DEFINE_ACTION_FUNCTION d2mira_copy BEGIN
  OUTER_SPRINT folder  ~%files%~
  ACTION_IF (GAME_IS ~iwd2~) BEGIN  OUTER_SPRINT folder  ~%iwd2%~  END    // iwd2 files (crashes with EE files)

  ACTION_PHP_EACH d2mira_copy AS res => copy BEGIN
    COPY ~%folder%/%copy%.itm~  ~override/%res%.itm~
      LPF d2mira_icons END
      LPF d2mira_ground END
    COPY ~%icons%/%copy%icon.bam~  ~override/%res%.bam~  IF_EXISTS
    COPY ~%icons%/%copy%desc.bam~  ~override/%res%d.bam~  IF_EXISTS
    COPY ~%icons%/%copy%ground.bam~  ~override/%res%g.bam~  IF_EXISTS
  END
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY d2mira_copy BEGIN    // mod folder res
  ~%bigmetalrod%~  => metalrod
  ~%pulse%~        => pulse
  ~%frag%~         => frag
  ~%scorcher%~     => scorcher
  ~%bigmetalunit%~ => metalunit
  ~%prototype%~    => prototype
  ~%pulseIII%~     => pulse3
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY d2mira_icons BEGIN    // icon res
  ~%bigmetalrod%~  => ~%bigmetalrod%~
  ~%pulse%~        => ~%pulse%~
  ~%frag%~         => ~%frag%~
  ~%scorcher%~     => ~%scorcher%~
  ~%bigmetalunit%~ => ~%bigmetalunit%~
  ~%prototype%~    => ~%pulse%~
  ~%pulseIII%~     => ~%pulse%~
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY d2mira_ground BEGIN    // icon res
  ~%bigmetalrod%~  => GSACK01
  ~%pulse%~        => GWAND01
  ~%frag%~         => GWAND01
  ~%scorcher%~     => GWAND01
  ~%bigmetalunit%~ => GPLAT01
  ~%prototype%~    => GWAND01
  ~%pulseIII%~     => GWAND01
END

OUTER_SPRINT $d2mira_ground(~%bigmetalrod%~) ~%bigmetalrod%g~
OUTER_SPRINT $d2mira_ground(~%bigmetalunit%~) ~%bigmetalunit%g~

DEFINE_PATCH_FUNCTION d2mira_icons BEGIN
  PHP_EACH d2mira_icons AS item => icon BEGIN
    PATCH_IF (~%res%~ STR_EQ ~%item%~) BEGIN
      WRITE_ASCII 0x3a ~%icon%~ #8     // icon
      WRITE_ASCII 0x58 ~%icon%d~ #8    // bg desc image
      LPF ALTER_ITEM_HEADER STR_VAR icon=~%icon%~ END
    END
  END
END

DEFINE_PATCH_FUNCTION d2mira_ground BEGIN
  PHP_EACH d2mira_ground AS item => icon BEGIN
    PATCH_IF (~%res%~ STR_EQ ~%item%~) BEGIN
      WRITE_ASCII 0x44 ~%icon%~ #8    // ground icon
    END
  END
END


//IWD pantaloons
DEFINE_ACTION_FUNCTION d2mira_pantaloons BEGIN
  ACTION_PHP_EACH d2mira_copypants AS res => copy BEGIN
    COPY ~%files%/%copy%.itm~  ~override/%res%.itm~
      LPF d2mira_iconspants END
    COPY ~%icons%/%copy%icon.bam~  ~override/%res%.bam~  IF_EXISTS
    COPY ~%icons%/%copy%desc.bam~  ~override/%res%d.bam~  IF_EXISTS
  END
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY d2mira_copypants BEGIN    // mod folder res
  ~%bronze%~ => bronzepants
  ~%silver%~ => silverpants
  ~%gold%~   => goldpants
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY d2mira_iconspants BEGIN    // icon res
  ~%bronze%~ => ~%bronze%~
  ~%silver%~ => ~%silver%~
  ~%gold%~   => ~%gold%~
END

DEFINE_PATCH_FUNCTION d2mira_iconspants BEGIN
  PHP_EACH d2mira_iconspants AS item => icon BEGIN
    PATCH_IF (~%res%~ STR_EQ ~%item%~) BEGIN
      WRITE_ASCII 0x3a ~%icon%~ #8     // icon
      WRITE_ASCII 0x58 ~%icon%d~ #8    // bg desc image
    END
  END
END



//
//text functions

//item text
DEFINE_ACTION_FUNCTION d2mira_bigmetal_text BEGIN
  COPY_EXISTING ~%bigmetalrod%.ITM~  ~override~
    SAY NAME1 ~%metalrod_name%~ SAY UNIDENTIFIED_DESC ~%metalrod_desc%~
    SAY NAME2 ~%metalrod_name%~ SAY DESC ~%metalrod_desc%~
    LPF d2mira_rodcolors INT_VAR colors=1 END
  COPY_EXISTING ~%pulse%.ITM~  ~override~
    SAY NAME1 ~%pulse_name%~ SAY UNIDENTIFIED_DESC ~%pulse_desc%~
    SAY NAME2 ~%pulse_name%~ SAY DESC ~%pulse_desc%~
  COPY_EXISTING ~%frag%.ITM~  ~override~
    SAY NAME1 ~%frag_name%~ SAY UNIDENTIFIED_DESC ~%frag_desc%~
    SAY NAME2 ~%frag_name%~ SAY DESC ~%frag_desc%~
  COPY_EXISTING ~%scorcher%.ITM~  ~override~
    SAY NAME1 ~%scorcher_name%~ SAY UNIDENTIFIED_DESC ~%scorcher_desc%~
    SAY NAME2 ~%scorcher_name%~ SAY DESC ~%scorcher_desc%~
  COPY_EXISTING ~%bigmetalunit%.ITM~  ~override~
    SAY NAME1 ~%combinedpants_name%~ SAY UNIDENTIFIED_DESC ~%combinedpants_desc%~
    SAY NAME2 ~%metalunit_name%~ SAY DESC ~%metalunit_desc%~
  COPY_EXISTING ~%prototype%.ITM~  ~override~
    SAY NAME1 ~%prototype_name%~ SAY UNIDENTIFIED_DESC ~%prototype_desc%~
    SAY NAME2 ~%prototype_name%~ SAY DESC ~%prototype_desc%~
  COPY_EXISTING ~%pulseIII%.ITM~  ~override~
    SAY NAME1 ~%pulseIII_name%~ SAY UNIDENTIFIED_DESC ~%pulseIII_desc%~
    SAY NAME2 ~%pulseIII_name%~ SAY DESC ~%pulseIII_desc%~
END

//Pantaloons
DEFINE_ACTION_FUNCTION d2mira_pantaloons_text BEGIN
  COPY_EXISTING ~%bronze%.ITM~  ~override~							
    SAY NAME1 ~%bronzepants_name%~ SAY UNIDENTIFIED_DESC ~%bronzepants_desc%~ 
    SAY NAME2 ~%bronzepants_name%~ SAY DESC ~%bronzepants_desc%~
  COPY_EXISTING ~%silver%.ITM~  ~override~							
    SAY NAME1 ~%silverpants_name%~ SAY UNIDENTIFIED_DESC ~%silverpants_desc%~ 
    SAY NAME2 ~%silverpants_name%~ SAY DESC ~%silverpants_desc%~
  COPY_EXISTING ~%gold%.ITM~  ~override~							
    SAY NAME1 ~%goldenpants_name%~ SAY UNIDENTIFIED_DESC ~%goldenpants_desc_unidentified%~ 
    SAY NAME2 ~%goldenpants_name%~ SAY DESC ~%goldenpants_desc%~
END

//edit metal rod colors
DEFINE_PATCH_FUNCTION d2mira_rodcolors INT_VAR colors = 0 BEGIN
  PATCH_IF (colors = 1) BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode=7 match_parameter2=21 parameter1=100 END
    LPF ALTER_EFFECT INT_VAR match_opcode=7 match_parameter2=20 parameter1=107 END
    LPF ALTER_EFFECT INT_VAR match_opcode=7 match_parameter2=16 parameter1=101 END
  END ELSE BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode=7 match_parameter2=21 parameter1=72 END
    LPF ALTER_EFFECT INT_VAR match_opcode=7 match_parameter2=20 parameter1=6 END
    LPF ALTER_EFFECT INT_VAR match_opcode=7 match_parameter2=16 parameter1=70 END
  END
END



//
//game-specific functions

//for BG1 (different text)
DEFINE_ACTION_FUNCTION d2mira_bg1alt_text BEGIN
  COPY_EXISTING ~%bigmetalrod%.ITM~  ~override/%metalrodb%.ITM~
    SAY NAME1 ~%metalrod_name%~ SAY UNIDENTIFIED_DESC ~%metalrod_desc_bg1%~
    SAY NAME2 ~%metalrod_name%~ SAY DESC ~%metalrod_desc_bg1%~
    WRITE_LONG 0x34 300    // 300 gold

  COPY_EXISTING ~%prototype%.ITM~  ~override/%prototypeb%.ITM~
    SAY NAME1 ~%prototype_name%~ SAY UNIDENTIFIED_DESC ~%prototype_desc_bg1%~
    SAY NAME2 ~%prototype_name%~ SAY DESC ~%prototype_desc_bg1%~
END

//Quest items (BG2)
DEFINE_ACTION_FUNCTION d2mira_bg2quest_text BEGIN
  COPY_EXISTING ~%bigmetalrod%.ITM~  ~override/%brokenrod%.ITM~
    SAY NAME1 ~%questitembg2_name%~ SAY UNIDENTIFIED_DESC ~%questitembg2_desc%~
    SAY NAME2 ~%questitembg2_name%~ SAY DESC ~%questitembg2_desc%~
    LPF DELETE_ITEM_HEADER INT_VAR header_type=~-1~ END
    WRITE_SHORT 0x1c 0    // change item type
    WRITE_LONG  0x34 0    // 0 gold
//    WRITE_BYTE 0x31 0     // no prof

  COPY ~%files%/repairmanual.ITM~  ~override/%repairbook%.ITM~
    SAY NAME1 ~%questbookbg2_name%~ SAY UNIDENTIFIED_DESC ~%questbookbg2_desc%~
    SAY NAME2 ~%questbookbg2_name%~ SAY DESC ~%questbookbg2_desc%~
END


//for classic BG2 engine
DEFINE_ACTION_FUNCTION d2mira_bg2core BEGIN
  ACTION_BASH_FOR ~%resource%~  ~.*~ BEGIN
    ACTION_IF !(FILE_EXISTS_IN_GAME ~%BASH_FOR_FILE%~) BEGIN    // add files if missing
      COPY ~%BASH_FOR_FILESPEC%~  ~override~
    END
  END

  // use BG2 files as defaults
  ACTION_PHP_EACH update_items AS mod => base BEGIN
    ACTION_IF (FILE_EXISTS ~override/%mod%.itm~) BEGIN
      COPY_EXISTING ~%base%.ITM~  ~override/%mod%.ITM~
        LPF ALTER_EFFECT INT_VAR silent=1 STR_VAR match_resource=~%SOURCE_RES%~ resource=~%DEST_RES%~ END
      IF_EXISTS
    END
  END

  ACTION_PHP_EACH d2mira_bg2images AS mod => base BEGIN
    COPY_EXISTING ~%base%.bam~  ~override/%mod%.bam~  IF_EXISTS
  END

  // tobex edits
  COPY_EXISTING ~%frag%.ITM~      ~override~
                ~%scorcher%.ITM~  ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode=12 savingthrow=16777218 END
    LPF ALTER_EFFECT INT_VAR multi_match=1 match_savingthrow=16777218 savingthrow=16777216 END
  BUT_ONLY

  // ground icons
  ACTION_PHP_EACH d2mira_ground AS item => icon BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~%item%g.bam~) BEGIN
      COPY_EXISTING ~%item%.itm~  ~override~
        WRITE_ASCII 0x44 ~%icon%~ #8
      IF_EXISTS BUT_ONLY
    END
  END
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY d2mira_bg2images BEGIN    // for original BG2
  ~%bigmetalrod%~   => ISECRT01
  ~%bigmetalrod%d~  => PSECRT01
  ~%pulse%~         => ISECRT02
  ~%pulse%d~        => PSECRT02
  ~%frag%~          => IMISC50
  ~%frag%d~         => CMISC50
  ~%scorcher%~      => ISECRT04
  ~%scorcher%d~     => PSECRT04
  ~%bigmetalunit%~  => ISECRT05
  ~%bigmetalunit%d~ => PSECRT05
END


//for EE fixpack
DEFINE_ACTION_FUNCTION d2mira_baseicons BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~secret05.itm~) BEGIN
    ACTION_PHP_EACH d2mira_copy AS res => copy BEGIN
      ACTION_PHP_EACH d2mira_bg2images AS item => icon BEGIN
        ACTION_IF (~%res%~ STR_EQ ~%item%~) BEGIN
          COPY_EXISTING ~%icon%.bam~  ~override/%res%.bam~  IF_EXISTS
        END
      END
    END
  END
END


//for IWD1 (classic)
DEFINE_ACTION_FUNCTION d2mira_iwdopcodes BEGIN
  COPY_EXISTING ~%bigmetalunit%.ITM~  ~override~
    LPF DELETE_EFFECT INT_VAR match_opcode=53 END    // no animation change (note: polymorph can't change appearance only)
    LPF ALTER_EFFECT INT_VAR match_opcode=292 opcode=287 END    // backstab immunity
  BUT_ONLY

  COPY_EXISTING ~%scorcher%.ITM~  ~override~
    LPF DELETE_EFFECT INT_VAR match_opcode=12 END    // no damage opcode (projectile has hardcoded damage)
  BUT_ONLY
END


//for IWD2
DEFINE_ACTION_FUNCTION d2mira_iwd2note BEGIN
  OUTER_SET note = RESOLVE_STR_REF (@49329276)    // note to reload after removing armor (fix selection sounds)
  COPY_EXISTING ~%metalunit%.itm~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=139 parameter1=note timing=4 duration=2 STR_VAR insert=last END
  BUT_ONLY
END


//for BG1 (classic)
DEFINE_ACTION_FUNCTION d2mira_bg1opcodes BEGIN
  COPY_EXISTING ~%bigmetalunit%.ITM~  ~override~
//    LPF DELETE_EFFECT INT_VAR match_opcode=53 END    // no animation change (no golem animation)
    LPF ALTER_EFFECT INT_VAR match_opcode=53 parameter1=0x7001 END    // ogrillon
//    LPF ALTER_EFFECT INT_VAR match_opcode=53 parameter1=0x2300 END    // death knight (test)
    LPF DELETE_EFFECT INT_VAR match_opcode=292 END    // no backstab immunity
    LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=7 parameter2=3 STR_VAR insert=last END    // ogrillon color
  BUT_ONLY

  ACTION_PHP_EACH d2mira_bg1images AS mod => base BEGIN
//    COPY_EXISTING ~%base%.bam~  ~override/%mod%.bam~  IF_EXISTS    // used for bams crashing (not needed anymore)
  END

  COPY ~%resource%/IMISC50.bam~  ~override/d2tec03.bam~    // use bg2 skull bam
END

//use for original BG1 (mod/bg2 icons crash)
ACTION_DEFINE_ASSOCIATIVE_ARRAY d2mira_bg1images BEGIN
  ~%bigmetalrod%~   => IXBOW06
  ~%bigmetalrod%d~  => CXBOW06
  ~%pulse%~         => IWAND03
  ~%pulse%d~        => CWAND03
  ~%frag%~          => IMISC50
  ~%frag%d~         => CMISC50
  ~%scorcher%~      => IWAND05
  ~%scorcher%d~     => CWAND05
  ~%bigmetalunit%~  => IPLAT08
  ~%bigmetalunit%d~ => CPLAT08
//
  ~%bigmetalrod%g~  => GSACK01
  ~%bigmetalunit%g~ => GPLAT01
END



//
//Install items

//copy items
LAF d2mira_copy END

/*
ACTION_IF (GAME_IS ~iwd2~) BEGIN
  COPY ~%iwd2%~  ~override~
END ELSE BEGIN
  LAF d2mira_copy END
END
*/


//BGEE, BG2EE
ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN
  LAF d2mira_bigmetal_text END
  LAF d2mira_bg1alt_text END
  LAF d2mira_bg2quest_text END
  LAF d2mira_baseicons END
END

//BG2 (classic)
ACTION_IF (GAME_IS ~bg2 tob tutu tutu_totsc ca~) BEGIN
  LAF d2mira_bg2core END
  LAF d2mira_bigmetal_text END
  LAF d2mira_bg1alt_text END
  LAF d2mira_bg2quest_text END
END

//BG1 (classic)
ACTION_IF (GAME_IS ~bg1 totsc~) BEGIN
  LAF d2mira_bg2core END
  LAF d2mira_bg1opcodes END
  LAF d2mira_bigmetal_text END
  LAF d2mira_bg1alt_text END
END

//IWDEE
ACTION_IF (GAME_IS ~iwdee~) BEGIN
  LAF d2mira_pantaloons END
  LAF d2mira_bigmetal_text END
  LAF d2mira_pantaloons_text END
  LAF d2mira_baseicons END
END

//IWD1
ACTION_IF (GAME_IS ~iwd how totlm~) BEGIN
  LAF d2mira_pantaloons END
  LAF d2mira_iwdopcodes END
  LAF d2mira_bigmetal_text END
  LAF d2mira_pantaloons_text END
END

//IWD2
ACTION_IF (GAME_IS ~iwd2~) BEGIN
  LAF d2mira_pantaloons END
  LAF d2mira_bigmetal_text END
  LAF d2mira_pantaloons_text END
  LAF d2mira_iwd2note END
END

//IWD-in-BG2
ACTION_IF (GAME_IS ~iwd_in_bg2~) BEGIN
  LAF d2mira_pantaloons END
  LAF d2mira_bg2core END
  LAF d2mira_bigmetal_text END
  LAF d2mira_pantaloons_text END
END



//
//extra stuff

//elemental weapons (for Better IWD Pregen scripts)
LAF d2_elemental_weapon_arrays STR_VAR item=~%frag%~ element=~fire~ RET_ARRAY d2fireweap END
LAF d2_elemental_weapon_arrays STR_VAR item=~%scorcher%~ element=~fire~ RET_ARRAY d2fireweap END


//set crossbow prof
LAF d2_get_weapontype_fields STR_VAR type=crossbow RET item_prof END
COPY_EXISTING ~%bigmetalrod%.itm~  ~override~  WRITE_BYTE 0x31  item_prof  IF_EXISTS BUT_ONLY
COPY_EXISTING ~%metalrodb%.itm~  ~override~  WRITE_BYTE 0x31  item_prof  IF_EXISTS BUT_ONLY
COPY_EXISTING ~%brokenrod%.itm~  ~override~  WRITE_BYTE 0x31  item_prof  IF_EXISTS BUT_ONLY


//polymorph check
ACTION_IF (ee_game > 0) BEGIN
  OUTER_TEXT_SPRINT d2polymorphcheck  ~d2polymorphcheck 59 1 4~     // POLYMORPHED >= 1
  LAF d2_splprot_adder STR_VAR match=d2polymorphcheck string=~%d2polymorphcheck%~ RET stat END
  OUTER_SET d2polymorphcheck = stat
END

//big metal unit animation
COPY_EXISTING ~%bigmetalunit%.itm~  ~override~
  // blocks other op53 effects (animation change)
  SET anim = 53
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=anim opcode=101 parameter2=anim STR_VAR insert=below END

  // bg2 (classic), block 1pp thing
  PATCH_IF (GAME_IS ~bg2 tob tutu tutu_totsc iwd_in_bg2 ca~) BEGIN
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=anim opcode=206 parameter1=0 STR_VAR resource=lcarmor END
  END

  // EEs: no animation if polymorphed
  PATCH_IF (IS_AN_INT d2polymorphcheck) BEGIN
    LPF d2_protect_on_castequip INT_VAR match_opcode=anim cre_type=d2polymorphcheck STR_VAR insert=above END
  END

  // npc bump
  PATCH_IF (RESOURCE_CONTAINS ~stats.ids~  ~NPC_BUMP~) BEGIN
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=anim opcode=300 parameter2=1 STR_VAR insert=below END
  END
BUT_ONLY


//
//