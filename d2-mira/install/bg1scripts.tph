//Standard Installation

/*
Variables, journal entry, store items for BGEE
*/


ACTION_IF (GAME_INCLUDES ~bg1~) BEGIN


//Set Global variables (check if party has items)
<<<<<<<< .../temp/variables
IF
	OR(2)
		PartyHasItem("%metalrodb%")
		PartyHasItem("%bigmetalrod%")
	!Global("d2HasBigMetalRod","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasBigMetalRod","GLOBAL",1)
		Continue()
END

IF
	OR(2)
		PartyHasItem("%prototypeb%")
		PartyHasItem("%prototype%")
	!Global("d2HasPrototype","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasPrototype","GLOBAL",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/variables~  ~inlined/variables.BAF~
  EVALUATE_BUFFER
  PATCH_IF (GAME_IS ~bg1 totsc~) BEGIN
    SPRINT match1  ~OR(2)~
    SPRINT match2  ~PartyHasItem("%bigmetalrod%")~
    SPRINT match3  ~PartyHasItem("%prototype%")~
    REPLACE_TEXTUALLY ~%match1%~  ~~
    REPLACE_TEXTUALLY ~%match2%~  ~~
    REPLACE_TEXTUALLY ~%match3%~  ~~
  END

EXTEND_TOP ~%baldursgate%.BCS~  ~inlined/variables.BAF~
EXTEND_TOP ~%candlekeep%.BCS~  ~inlined/variables.BAF~



//
//BG1: Black Lily journal entry
<<<<<<<< .../temp/blacklily
IF
	Global("d2BlackLilyTalk","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2BlackLilyTalk","GLOBAL",2)
		SetGlobalTimer("d2JournalDelay","GLOBAL",3)
		Continue()
END

IF
	Global("d2BlackLilyTalk","GLOBAL",2)
	!GlobalTimerNotExpired("d2JournalDelay","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("d2BlackLilyTalk","GLOBAL",3)
		AddJournalEntry(%bg1jour01%)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/blacklily~  ~inlined/blacklily.BAF~
  EVALUATE_BUFFER

EXTEND_TOP ~%baldursgate%.BCS~  ~inlined/blacklily.BAF~
EXTEND_TOP ~%thiefguild%.BCS~  ~inlined/blacklily.BAF~



//
//BGEE: add Metal Rod (note: AddStoreItem is EE-only)

/*add to store by script (old version)
ACTION_IF (RESOURCE_CONTAINS ~ACTION.IDS~ ~AddStoreItem~) BEGIN

<<<<<<<< .../temp/luckystore
IF
	GlobalGT("d2BlackLilyTalk","GLOBAL",0)
	!Global("d2LuckyAelloBigMetal","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2LuckyAelloBigMetal","GLOBAL",1)
		AddStoreItem("STOCHEAP","%metalrodb%",1,1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/luckystore~  ~inlined/luckystore.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~%baldursgate%.BCS~  ~inlined/luckystore.BAF~
EXTEND_BOTTOM ~%luckystore%.BCS~  ~inlined/luckystore.BAF~

END
*/

//
//add to container in store (new version)
COPY_EXISTING ~%luckystore%.ARE~  ~override~
  LPF read_or_set_areascript RET areascript END
BUT_ONLY

<<<<<<<< .../temp/luckybarrel
IF
	GlobalGT("d2BlackLilyTalk","GLOBAL",0)
	!Global("d2LuckyAelloBigMetal","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2LuckyAelloBigMetal","GLOBAL",1)
		ActionOverride("Container3",CreateItem("%metalrodb%",0,0,0))    // Container3 is non-trapped
		Continue()
END
>>>>>>>>

COPY - ~.../temp/luckybarrel~  ~inlined/luckybarrel.BAF~
//PATCH_IF (GAME_IS ~bg1 totsc~) BEGIN
    REPLACE_TEXTUALLY ~ActionOverride~  ~//~    // this is now done from below script (d2mtluck)
//END
  EVALUATE_BUFFER

EXTEND_BOTTOM ~%areascript%.BCS~  ~inlined/luckybarrel.BAF~



//
//for Lucky Aello talk
COPY_EXISTING ~LUCKY.cre~  ~override~
  LPF read_or_set_scriptname RET scriptname END
BUT_ONLY

//these are changed to play with text strings (more reliable than scripting)
OUTER_SPRINT folder  ~%resource%~
ACTION_IF (ee_game > 0) BEGIN  OUTER_SPRINT folder  ~%files%~  END
ACTION_IF !(FILE_EXISTS_IN_GAME ~itemharp.wav~) BEGIN  COPY ~%folder%/itemharp.wav~ ~override~  END
ACTION_IF !(FILE_EXISTS_IN_GAME ~itemhorn.wav~) BEGIN  COPY ~%folder%/itemhorn.wav~ ~override~  END

<<<<<<<< .../temp/d2mtluck
IF
	True()
THEN
	RESPONSE #100
		Wait(1)
		FadeToColor([45.0],0)
        %stuff%    // bg1 only
		Wait(2)
		FadeFromColor([45.0],0)
		Wait(1)
		ActionOverride("%scriptname%",StartDialogueNoSet(LastTalkedToBy))
END
>>>>>>>>

COPY - ~.../temp/d2mtluck~  ~inlined/d2mtluck.BAF~
  TEXT_SPRINT stuff  ~~
//PATCH_IF (GAME_IS ~bg1 totsc~) BEGIN
    TEXT_SPRINT stuff ~ActionOverride("Container3",CreateItem("%metalrodb%",0,0,0))~
//END
  EVALUATE_BUFFER

COMPILE ~inlined/d2mtluck.BAF~



//
//SoD: Journal entry
<<<<<<<< .../temp/sodhalbaz
IF
	Global("d2HalbazBigMetal","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HalbazBigMetal","GLOBAL",2)
		SetGlobalTimer("d2JournalDelay","GLOBAL",5)
		Continue()
END

IF
	!Global("d2HasPrototype","GLOBAL",1)
	Global("d2HalbazBigMetal","GLOBAL",2)
	!GlobalTimerNotExpired("d2JournalDelay","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("d2HalbazBigMetal","GLOBAL",3)
		AddJournalEntry(%bg1jour02%)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/sodhalbaz~  ~inlined/sodhalbaz.BAF~
  EVALUATE_BUFFER

ACTION_IF (FILE_EXISTS_IN_GAME ~bd0020.ARE~) BEGIN
  EXTEND_TOP ~bd0020.BCS~  ~inlined/sodhalbaz.BAF~
  EXTEND_TOP ~bd0020.BCS~  ~inlined/variables.BAF~
END



//
//SoD: Add Rod and/or Pulse (prototype)
<<<<<<<< .../temp/RodPulse
IF
	!PartyHasItem("%metalrodb%")
	!PartyHasItem("%bigmetalrod%")
	!Global("d2HasBigMetalRod","GLOBAL",1)
	!Global("d2BelegarmBigMetal","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2BelegarmBigMetal","GLOBAL",1)
		ActionOverride("PlayerChest00",CreateItem("%bigmetalrod%",0,0,0))
//		AddStoreItem("BDBELEG4","%bigmetalrod%",1,1)
		Continue()
END

IF
	!PartyHasItem("%prototypeb%")
	!PartyHasItem("%prototype%")
	!Global("d2HasPrototype","GLOBAL",1)
	!Global("d2BelegarmPrototype","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2BelegarmPrototype","GLOBAL",1)
		ActionOverride("PlayerChest00",CreateItem("%prototype%",0,0,0))
//		AddStoreItem("BDBELEG4","%prototype%",1,1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/RodPulse~  ~inlined/siegecamp.BAF~
  EVALUATE_BUFFER

ACTION_IF (FILE_EXISTS_IN_GAME ~bd3000.ARE~) BEGIN
  EXTEND_TOP ~bd3000.BCS~ ~inlined/siegecamp.BAF~
  EXTEND_TOP ~bd3000.BCS~ ~inlined/variables.BAF~
END



//
//reset variables for SoD (will be set back to 1 if party has items)
<<<<<<<< .../temp/resetvar
IF
	!Global("d2mira_sodreset","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasBigMetalRod","GLOBAL",0)
		SetGlobal("d2HasPrototype","GLOBAL",0)
		SetGlobal("d2mira_sodreset","GLOBAL",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/resetvar~  ~inlined/resetvar.baf~
  EVALUATE_BUFFER

ACTION_IF (FILE_EXISTS_IN_GAME ~bdbaldur.bcs~) BEGIN
  EXTEND_BOTTOM ~bdbaldur.bcs~  ~inlined/resetvar.baf~
END


END


/*
*/