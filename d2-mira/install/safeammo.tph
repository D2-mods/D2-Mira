/*
Make ammo more party friendly
*/


//EEs: Applies to Scorcher and Frag Grenade
//IWD games: Applies to Frag Grenade only (can't make it work with Scorcher projectile)


ACTION_IF (FILE_EXISTS engine.lua && RESOURCE_CONTAINS ~action.ids~ ~zoomlock~) BEGIN
  OUTER_SET effect_to_use = 318
  OUTER_SET effect_target = 52
  OUTER_SET target_type   = 0
END
ELSE ACTION_IF (GAME_IS ~iwd2 iwd how totlm~) BEGIN
  OUTER_SET effect_to_use = 206
  OUTER_SET effect_target = 52
  OUTER_SET target_type   = 0
END
ELSE BEGIN
  OUTER_SET effect_to_use = 177
  OUTER_SET effect_target = 2
  OUTER_SET target_type   = 255
END


//
ACTION_FOR_EACH ammo_type IN
 ~%frag%~
 ~%scorcher%~
BEGIN

  COPY_EXISTING ~%ammo_type%.ITM~  ~override~
    LPF ADD_ITEM_EFFECT
      INT_VAR
        opcode        = %effect_to_use%    // opcode
        target        = 2
        parameter1    = %target_type%      // for op177 (classic BG2 engine)
        parameter2    = %effect_target%    // set which targets are immune
        type          = 2
        insert_point  = 0
      STR_VAR
        resource      = ~%ammo_type%~
    END

    PATCH_IF (effect_to_use = 177) BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode=12 END
      LPF ALTER_EFFECT INT_VAR match_opcode=177 STR_VAR match_resource=~%ammo_type%~ resource=~%ammo_type%a~ END
    END
  BUT_ONLY

  ACTION_IF (~%ammo_type%~ STR_EQ ~%frag%~) BEGIN  OUTER_SET dice=3 OUTER_SET roll=4 END    // frag does 3d4
  ACTION_IF (~%ammo_type%~ STR_EQ ~%scorcher%~) BEGIN  OUTER_SET dice=1 OUTER_SET roll=12 END    // scorcher does 2d12

  ACTION_IF (effect_to_use = 177) BEGIN
    COPY ~%files%/bg2safeammo.EFF~  ~override/%ammo_type%a.EFF~
      WRITE_SHORT 0x38 dice
      WRITE_SHORT 0x3c roll
  END

END


//
//classic BG2: Scorcher allows save for half
ACTION_IF (effect_to_use = 177) BEGIN
  COPY ~%files%/bg2safescorch.EFF~  ~override/%scorcher%b.EFF~
  COPY_EXISTING ~%scorcher%.ITM~  ~override~  
    LPF CLONE_EFFECT STR_VAR match_resource=~%scorcher%a~ resource=~%scorcher%b~ END
  BUT_ONLY
END


//classic BG2: check Frag Grenade (damage type and rolls)
ACTION_IF (effect_to_use = 177) BEGIN
  COPY_EXISTING ~%frag%a.EFF~  ~override~
    PATCH_IF (MOD_IS_INSTALLED ~d2-mira.tp2~ 31) BEGIN    // if slashing damage
      WRITE_SHORT 0x22 256
    END
    PATCH_IF (MOD_IS_INSTALLED ~d2-mira.tp2~ 11) BEGIN    // if vanilla BG2 stats
      WRITE_SHORT 0x38 1 WRITE_SHORT 0x3c 12
    END
  BUT_ONLY
END



//
//Update BG2 files
INCLUDE ~%install%/updatebg2.tph~


/*
*/