//Add Delivery Bag
//

/*
Resource: d2#mbag.ITM
*/


ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN
  COPY ~%mbag%/mbag-ee.BAM~  ~override/%mirabag%.BAM~
  COPY ~%mbag%/d2hbee1.ITM~  ~override/%mirabag%.ITM~
  COPY ~%mbag%/d2hbee1.STO~  ~override/%mirabag%.STO~
    SAY 0xc ~%mirabag_name%~  /* Delivery Bag */
END

ACTION_IF (GAME_IS ~iwdee~) BEGIN
  COPY ~%mbag%/mbag-ee.BAM~  ~override/%mirabag%.BAM~
  COPY ~%mbag%/d2hbee0.ITM~  ~override/%mirabag%.ITM~
  COPY ~%mbag%/d2hbee0.STO~  ~override/%mirabag%.STO~
    SAY 0xc ~%mirabag_name%~  /* Delivery Bag */
END

ACTION_IF (GAME_IS ~how totlm~) BEGIN
  COPY ~%mbag%/mbag-id1.BAM~  ~override/%mirabag%.BAM~
  COPY ~%mbag%/d2hbid1.ITM~  ~override/%mirabag%.ITM~
  COPY ~%mbag%/d2hbid1.STO~  ~override/%mirabag%.STO~
    SAY 0xc ~%mirabag_name%~  /* Delivery Bag */
END

ACTION_IF (GAME_IS ~iwd2~) BEGIN
  COPY ~%mbag%/mbag-id2.BAM~  ~override/%mirabag%.BAM~
  COPY ~%mbag%/d2hbid2.ITM~  ~override/%mirabag%.ITM~
  COPY ~%mbag%/d2hbid2.STO~  ~override/%mirabag%.STO~
    SAY 0xc ~%mirabag_name%~  /* Delivery Bag */
END

ACTION_IF (GAME_IS ~bg2 tob tutu tutu_totsc ca iwd_in_bg2~) BEGIN
  COPY ~%mbag%/mbag-bg2.BAM~  ~override/%mirabag%.BAM~
  COPY ~%mbag%/d2hbbg2.ITM~  ~override/%mirabag%.ITM~
  COPY ~%mbag%/d2hbbg2.STO~  ~override/%mirabag%.STO~
    SAY 0xc ~%mirabag_name%~  /* Delivery Bag */
END


COPY_EXISTING ~%mirabag%.ITM~  ~override~
  SAY NAME1 ~%mirabag_name%~ SAY UNIDENTIFIED_DESC ~%mirabag_desc%~
  SAY NAME2 ~%mirabag_name%~ SAY DESC ~%mirabag_desc%~
  WRITE_ASCII 0x3a ~%mirabag%~ #8    // set inventory icon
BUT_ONLY

COPY_EXISTING ~%mirabag%.STO~  ~override/%mirabag%1.STO~    // backup for later tweak
COPY_EXISTING ~%mirabag%.STO~  ~override~
  LPF cannot_add_items END    // make it so items cannot be placed in the bag
BUT_ONLY


INCLUDE ~%MOD_FOLDER%/install/mbagfill.tph~



//
//Add scripts (for "Start with core items" option)

ACTION_IF (mira = 2) BEGIN

  ACTION_IF (GAME_INCLUDES ~bg2~) BEGIN
    EXTEND_BOTTOM ~AR0602.BCS~  ~%mbag%/mbag-imoen.BAF~
  END 
  
  ACTION_IF (GAME_INCLUDES ~sod~) BEGIN
    EXTEND_BOTTOM ~bd0120.BCS~  ~%mbag%/mbag-sodscript.BAF~
  END

  ACTION_FOR_EACH script IN ~BPHUB~ ~ohbhub~ BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~%script%.BCS~) BEGIN
      EXTEND_BOTTOM ~%script%.BCS~  ~%mbag%/mbag-bpscript.BAF~
    END
  END

  ACTION_FOR_EACH script IN ~BALDUR~ ~BALDUR25~ ~bdbaldur~ BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~%script%.BCS~) BEGIN
      EXTEND_TOP ~%script%.BCS~  ~%mbag%/mbag-script.BAF~
    END
  END

  ACTION_IF (FILE_EXISTS ~override/lhMIS0a.itm~) BEGIN
    EXTEND_BOTTOM ~BALDUR.BCS~ ~%mbag%/mbag-epicadv.BAF~
  END
  
  ACTION_IF (GAME_IS ~ca~) BEGIN
    EXTEND_BOTTOM ~TC0009.BCS~ ~%mbag%/mbag-ca.BAF~
  END

END


/*
*/