/*
Add Delivery Bag
Resource: d2#mbag.ITM
*/


COPY ~%mbag%/d2#mbag.BAM~  ~override/%mirabag%.BAM~


//add files
ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN
  COPY ~%mbag%/d2hbee1.ITM~  ~override/%mirabag%.ITM~
  COPY ~%mbag%/d2hbee1.STO~  ~override/%mirabag%.STO~
    SAY 0xc ~%mirabag_name%~  /* Delivery Bag */
END

ACTION_IF (GAME_IS ~iwdee~) BEGIN
  COPY ~%mbag%/d2hbee0.ITM~  ~override/%mirabag%.ITM~
  COPY ~%mbag%/d2hbee0.STO~  ~override/%mirabag%.STO~
    SAY 0xc ~%mirabag_name%~  /* Delivery Bag */
END

ACTION_IF (GAME_IS ~how totlm~) BEGIN
  COPY ~%mbag%/d2hbid1.ITM~  ~override/%mirabag%.ITM~
  COPY ~%mbag%/d2hbid1.STO~  ~override/%mirabag%.STO~
    SAY 0xc ~%mirabag_name%~  /* Delivery Bag */
END

ACTION_IF (GAME_IS ~iwd2~) BEGIN
  COPY ~%mbag%/d2hbid2.ITM~  ~override/%mirabag%.ITM~
  COPY ~%mbag%/d2hbid2.STO~  ~override/%mirabag%.STO~
    SAY 0xc ~%mirabag_name%~  /* Delivery Bag */
END

ACTION_IF (GAME_IS ~bg2 tob tutu tutu_totsc ca iwd_in_bg2~) BEGIN
  COPY ~%mbag%/d2hbbg2.ITM~  ~override/%mirabag%.ITM~
  COPY ~%mbag%/d2hbbg2.STO~  ~override/%mirabag%.STO~
    SAY 0xc ~%mirabag_name%~  /* Delivery Bag */
END



//variants for bg1 and iwd1 (with no HoW)

/*
used as quick items (these games don't support bags)
items are added automatically to the inventory
*/

//OUTER_TEXT_SPRINT checkbag  ~%mirabag%t~
//this is set in prep.tpa

ACTION_IF (GAME_IS ~bg1 totsc~) BEGIN    // note: bg1 does not support bags
  COPY ~%mbag%/quickitembg1.ITM~  ~override/%mirabag%.ITM~
    WRITE_SHORT 0x1c 0
    LPF ALTER_ITEM_HEADER STR_VAR icon=~%mirabag%~ END
    LPF ALTER_EFFECT STR_VAR resource=~%checkbag%~ END
  COPY ~%mbag%/d2hbbg2.STO~       ~override/%mirabag%.STO~
    SAY 0xc ~%mirabag_name%~  /* Delivery Bag */
    WRITE_LONG 0x8 0
END

ACTION_IF (GAME_IS ~iwd~) BEGIN
  COPY ~%mbag%/quickitemiwd.ITM~  ~override/%mirabag%.ITM~
    WRITE_SHORT 0x1c 0
    LPF ALTER_ITEM_HEADER STR_VAR icon=~%mirabag%~ END
    LPF ALTER_EFFECT STR_VAR resource=~%checkbag%~ END
  COPY ~%mbag%/d2hbid1.STO~       ~override/%mirabag%.STO~
    SAY 0xc ~%mirabag_name%~  /* Delivery Bag */
    WRITE_LONG 0x8 0
END



//
COPY_EXISTING ~%mirabag%.ITM~  ~override~
  SAY NAME1 ~%mirabag_name%~ SAY UNIDENTIFIED_DESC ~%mirabag_desc%~
  SAY NAME2 ~%mirabag_name%~ SAY DESC ~%mirabag_desc%~
  WRITE_ASCII 0x3a ~%mirabag%~ #8    // set inventory icon
BUT_ONLY

COPY_EXISTING ~%mirabag%.STO~  ~override/%mirabag%1.STO~    // backup for later tweak
COPY_EXISTING ~%mirabag%.STO~  ~override~
  LPF cannot_add_items END    // make it so items cannot be placed in the bag
BUT_ONLY


INCLUDE ~%MOD_FOLDER%/install/mbagfill.tph~



//
//Add scripts (for "Start with core items" option)

ACTION_IF (mira = 2) BEGIN

  ACTION_IF (GAME_INCLUDES ~bg2~) BEGIN
    EXTEND_BOTTOM ~AR0602.BCS~  ~%mbag%/mbag-imoen.BAF~
  END 

  ACTION_IF (GAME_INCLUDES ~sod~) BEGIN
    EXTEND_BOTTOM ~bd0120.BCS~  ~%mbag%/mbag-sodscript.BAF~
  END

  ACTION_FOR_EACH script IN ~BPHUB~ ~ohbhub~ BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~%script%.BCS~) BEGIN
      EXTEND_BOTTOM ~%script%.BCS~  ~%mbag%/mbag-bpscript.BAF~
    END
  END

  ACTION_FOR_EACH script IN ~BALDUR~ ~BALDUR25~ ~bdbaldur~ BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~%script%.BCS~) BEGIN
      EXTEND_TOP ~%script%.BCS~  ~%mbag%/mbag-script.BAF~
    END
  END

  ACTION_IF (FILE_EXISTS ~override/lhMIS0a.itm~) BEGIN
    EXTEND_BOTTOM ~BALDUR.BCS~ ~%mbag%/mbag-epicadv.BAF~
  END

  ACTION_IF (GAME_IS ~ca~) BEGIN
    EXTEND_BOTTOM ~TC0009.BCS~ ~%mbag%/mbag-ca.BAF~
  END

  // bg1 has no global script (baldur.bcs), add to each area script
  ACTION_IF (GAME_IS ~bg1 totsc~) BEGIN
    COPY_EXISTING_REGEXP - GLOB ~.*\.BCS~  ~override~
      PATCH_IF (FILE_EXISTS_IN_GAME ~%SOURCE_RES%.are~) BEGIN
        TEXT_SPRINT $d2mira_bg1bag(~%SOURCE_RES%~) ~script~
      END

    ACTION_PHP_EACH d2mira_bg1bag AS script => extend BEGIN
      EXTEND_TOP ~%script%.bcs~  ~%mbag%/mbag-script.BAF~
    END
  END

END


//
//