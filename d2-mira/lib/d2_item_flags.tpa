//functions for items specifically
//all can be used as PATCH or ACTION, unless noted otherwise


/*functions:
d2_magic_item_flags    - set or remove enchantment flags
d2_silverweapon        - set flags for silver or gold/coldiron (use for melee weapons or ammo)
d2_silverweapon_buff   - EE-only, adds as equipped effects (use for launchers or non-weapons)
d2_droppableitems      - add or remove droppable flag (usable by PCs or not)
d2_soddrowitems        - add or remove adamantine flag (only relevant for SoD)
d2_equip_statreq       - stat requirements for items (Str, Dex, etc.)
d2_pstitemflags        - common pst flags (steel, conversable, usable in inventory)
d2_pstitem_npcflags    - remove npc flags for pstee (allows using ability from inventory)
d2_no_usableflags      - remove usability flags
d2_item_race_flags     - add/remove race flags (PATCH only)
*/

////////////////////////////////////////////////////////////

/*presets
"magical" enabled by default, "cursed" and "critical" disabled by default
others are set to -1 (no change)
if "magical" is set and patching an ITM with 0 enchantment, "enchantment" is set to at least +1 (if not set higher manually)
if "enchantment" is set to 1+, the "magical" flag will always be enabled by function
*/

DEFINE_ACTION_FUNCTION d2_magic_item_flags
  INT_VAR magical=1 cursed=0 critical=0 enchantment=~-1~
          undispellable=~-1~ silver=~-1~ gold=~-1~ coldiron=~-1~
  STR_VAR item=~~ resource=~~
BEGIN
  ACTION_FOR_EACH file IN ~%item%~ ~%resource%~ BEGIN
    COPY_EXISTING ~%file%.ITM~  ~override~
      // enchantment level
      PATCH_IF (magical>0 AND enchantment<1) BEGIN
        READ_LONG 0x60 enchant_level
        PATCH_IF (enchant_level = 0) BEGIN  SET enchantment = 1 END
      END
      PATCH_IF (magical<1 AND enchantment>0) BEGIN  SET magical = 1  END
      PATCH_IF (enchantment > 6) BEGIN  SET enchantment = 6  END
      PATCH_IF (enchantment >= 0) BEGIN  WRITE_LONG 0x60 enchantment  END

      // set flag
      PATCH_IF (magical > 0)          BEGIN  WRITE_LONG 0x18 THIS | BIT6  END
      PATCH_IF (cursed > 0)           BEGIN  WRITE_LONG 0x18 THIS | BIT4  END
      PATCH_IF (undispellable > 0)    BEGIN  WRITE_LONG 0x18 THIS | BIT24  END
      PATCH_IF (critical > 0)         BEGIN  WRITE_LONG 0x18 THIS | BIT0  END
      PATCH_IF (silver > 0)           BEGIN  WRITE_LONG 0x18 THIS | BIT8  END
      PATCH_IF (gold>0 OR coldiron>0) BEGIN  WRITE_LONG 0x18 THIS | BIT9  END

      // remove flag
      PATCH_IF (magical = 0)          BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT6)  END
      PATCH_IF (cursed = 0)           BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT4)  END
      PATCH_IF (undispellable = 0)    BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT24)  END
      PATCH_IF (critical = 0)         BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT0)  END
      PATCH_IF (silver = 0)           BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT8)  END
      PATCH_IF (gold=0 OR coldiron=0) BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT9)  END
    IF_EXISTS BUT_ONLY
  END
END

DEFINE_PATCH_FUNCTION d2_magic_item_flags
  INT_VAR magical=1 cursed=0 critical=0 enchantment=~-1~
          undispellable=~-1~ silver=~-1~ gold=~-1~ coldiron=~-1~
BEGIN
  PATCH_IF (~%DEST_EXT%~ STR_EQ ~itm~) BEGIN
      // enchantment level
      PATCH_IF (magical>0 AND enchantment<1) BEGIN
        READ_LONG 0x60 enchant_level
        PATCH_IF (enchant_level = 0) BEGIN  SET enchantment = 1 END
      END
      PATCH_IF (magical<1 AND enchantment>0) BEGIN  SET magical = 1  END
      PATCH_IF (enchantment > 6) BEGIN  SET enchantment = 6  END
      PATCH_IF (enchantment >= 0) BEGIN  WRITE_LONG 0x60 enchantment  END

      // set flag
      PATCH_IF (magical > 0)          BEGIN  WRITE_LONG 0x18 THIS | BIT6  END
      PATCH_IF (cursed > 0)           BEGIN  WRITE_LONG 0x18 THIS | BIT4  END
      PATCH_IF (undispellable > 0)    BEGIN  WRITE_LONG 0x18 THIS | BIT24  END
      PATCH_IF (critical > 0)         BEGIN  WRITE_LONG 0x18 THIS | BIT0  END
      PATCH_IF (silver > 0)           BEGIN  WRITE_LONG 0x18 THIS | BIT8  END
      PATCH_IF (gold>0 OR coldiron>0) BEGIN  WRITE_LONG 0x18 THIS | BIT9  END

      // remove flag
      PATCH_IF (magical = 0)          BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT6)  END
      PATCH_IF (cursed = 0)           BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT4)  END
      PATCH_IF (undispellable = 0)    BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT24)  END
      PATCH_IF (critical = 0)         BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT0)  END
      PATCH_IF (silver = 0)           BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT8)  END
      PATCH_IF (gold=0 OR coldiron=0) BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT9)  END
  END
END

////////////////////////////////////////////////////////////

//silver and gold/coldiron are enabled by default
//if either gold or coldiron is set to 0, it is NOT added

DEFINE_ACTION_FUNCTION d2_silverweapon INT_VAR silver=1 gold=1 coldiron=1 STR_VAR item=~~ resource=~~ BEGIN
  ACTION_FOR_EACH file IN ~%item%~ ~%resource%~ BEGIN
    COPY_EXISTING ~%file%.ITM~  ~override~
      PATCH_IF (silver > 0)            BEGIN  WRITE_LONG 0x18 THIS | BIT8  END
      PATCH_IF (gold>0 AND coldiron>0) BEGIN  WRITE_LONG 0x18 THIS | BIT9  END
    IF_EXISTS BUT_ONLY
  END
END

DEFINE_PATCH_FUNCTION d2_silverweapon INT_VAR silver=1 gold=1 coldiron=1 BEGIN
  PATCH_IF (~%DEST_EXT%~ STR_EQ ~itm~) BEGIN
      PATCH_IF (silver > 0)            BEGIN  WRITE_LONG 0x18 THIS | BIT8  END
      PATCH_IF (gold>0 AND coldiron>0) BEGIN  WRITE_LONG 0x18 THIS | BIT9  END
  END
END

////////////////////////////////////////////////////////////

//"mode" variable is the special field in effect (default 3 = all attacks)
//will overwrite existing effects (won't duplicate effects)

DEFINE_ACTION_FUNCTION d2_silverweapon_buff INT_VAR silver=1 gold=1 coldiron=1 mode=3 STR_VAR item=~~ resource=~~ BEGIN
  ACTION_FOR_EACH file IN ~%item%~ ~%resource%~ BEGIN
    COPY_EXISTING ~%file%.ITM~  ~override~
      PATCH_IF (silver > 0) BEGIN
        LPF DELETE_EFFECT INT_VAR match_opcode=345 match_parameter2=3 END
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode=345 target=1 timing=2 parameter2=3 special=mode insert_point=~-1~ END
      END
      PATCH_IF (gold>0 AND coldiron>0) BEGIN
        LPF DELETE_EFFECT INT_VAR match_opcode=345 match_parameter2=10 END
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode=345 target=1 timing=2 parameter2=10 special=mode insert_point=~-1~ END
      END
    IF_EXISTS BUT_ONLY
  END
END

DEFINE_PATCH_FUNCTION d2_silverweapon_buff INT_VAR silver=1 gold=1 coldiron=1 mode=3 BEGIN
  PATCH_IF (~%DEST_EXT%~ STR_EQ ~itm~) BEGIN
    PATCH_IF (silver > 0) BEGIN
        LPF DELETE_EFFECT INT_VAR match_opcode=345 match_parameter2=3 END
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode=345 target=1 timing=2 parameter2=3 special=mode insert_point=~-1~ END
    END
    PATCH_IF (gold>0 AND coldiron>0) BEGIN
        LPF DELETE_EFFECT INT_VAR match_opcode=345 match_parameter2=10 END
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode=345 target=1 timing=2 parameter2=10 special=mode insert_point=~-1~ END
    END
  END
END

////////////////////////////////////////////////////////////

//makes item usable by PCs (droppable and displayable)
//set "mode" variable to "remove" or "delete" to remove flag (only removes droppable)

DEFINE_ACTION_FUNCTION d2_droppableitems STR_VAR item=~~ resource=~~ mode=add BEGIN
  ACTION_FOR_EACH file IN ~%item%~ ~%resource%~ BEGIN
    COPY_EXISTING ~%file%.ITM~  ~override~
      PATCH_IF ((~%mode%~ STR_EQ ~remove~) OR (~%mode%~ STR_EQ ~delete~)) BEGIN
        WRITE_LONG 0x18 (THIS BAND BNOT BIT2)
      END ELSE BEGIN
        WRITE_LONG 0x18 THIS | BIT2
        WRITE_LONG 0x18 THIS | BIT3
      END
    IF_EXISTS BUT_ONLY
  END
END

DEFINE_PATCH_FUNCTION d2_droppableitems STR_VAR mode=add BEGIN
  PATCH_IF (~%DEST_EXT%~ STR_EQ ~itm~) BEGIN
      PATCH_IF ((~%mode%~ STR_EQ ~remove~) OR (~%mode%~ STR_EQ ~delete~)) BEGIN
        WRITE_LONG 0x18 (THIS BAND BNOT BIT2)
      END ELSE BEGIN
        WRITE_LONG 0x18 THIS | BIT2
        WRITE_LONG 0x18 THIS | BIT3
      END
  END
END

////////////////////////////////////////////////////////////

//destroyed by DestroyAllFragileEquipment script action (only relevant for SoD)
//set "mode" variable to "remove" or "delete" to remove flag

DEFINE_ACTION_FUNCTION d2_soddrowitems STR_VAR item=~~ resource=~~ mode=add BEGIN
  ACTION_FOR_EACH file IN ~%item%~ ~%resource%~ BEGIN
    COPY_EXISTING ~%file%.ITM~  ~override~
      PATCH_IF ((~%mode%~ STR_EQ ~remove~) OR (~%mode%~ STR_EQ ~delete~)) BEGIN
        WRITE_LONG 0x18 (THIS BAND BNOT BIT15)
      END ELSE BEGIN
        WRITE_LONG 0x18 THIS | BIT15
      END
    IF_EXISTS BUT_ONLY
  END
END

DEFINE_PATCH_FUNCTION d2_soddrowitems STR_VAR mode=add BEGIN
  PATCH_IF (~%DEST_EXT%~ STR_EQ ~itm~) BEGIN
      PATCH_IF ((~%mode%~ STR_EQ ~remove~) OR (~%mode%~ STR_EQ ~delete~)) BEGIN
        WRITE_LONG 0x18 (THIS BAND BNOT BIT15)
      END ELSE BEGIN
        WRITE_LONG 0x18 THIS | BIT15
      END
  END
END

////////////////////////////////////////////////////////////

//"stat" variable can be a list (ex. stat = ~str dex int~)
//sets to 0 if "amount" variable is not set

DEFINE_ACTION_FUNCTION d2_equip_statreq INT_VAR amount=0 STR_VAR item=~~ resource=~~ stat=none BEGIN
  ACTION_FOR_EACH file IN ~%item%~ ~%resource%~ BEGIN
    COPY_EXISTING ~%file%.ITM~  ~override~
      PATCH_IF !(~%stat%~ STRING_CONTAINS_REGEXP ~str~) BEGIN  WRITE_SHORT 0x26 amount  END
      PATCH_IF !(~%stat%~ STRING_CONTAINS_REGEXP ~dex~) BEGIN  WRITE_BYTE 0x2c amount  END
      PATCH_IF !(~%stat%~ STRING_CONTAINS_REGEXP ~con~) BEGIN  WRITE_BYTE 0x30 amount  END
      PATCH_IF !(~%stat%~ STRING_CONTAINS_REGEXP ~int~) BEGIN  WRITE_BYTE 0x2a amount  END
      PATCH_IF !(~%stat%~ STRING_CONTAINS_REGEXP ~wis~) BEGIN  WRITE_BYTE 0x2e amount  END
      PATCH_IF !(~%stat%~ STRING_CONTAINS_REGEXP ~cha~) BEGIN  WRITE_SHORT 0x32 amount  END
    IF_EXISTS BUT_ONLY
  END
END

DEFINE_PATCH_FUNCTION d2_equip_statreq INT_VAR amount=0 STR_VAR stat=none BEGIN
  PATCH_IF (~%DEST_EXT%~ STR_EQ ~itm~) BEGIN
      PATCH_IF !(~%stat%~ STRING_CONTAINS_REGEXP ~str~) BEGIN  WRITE_SHORT 0x26 amount  END
      PATCH_IF !(~%stat%~ STRING_CONTAINS_REGEXP ~dex~) BEGIN  WRITE_BYTE 0x2c amount  END
      PATCH_IF !(~%stat%~ STRING_CONTAINS_REGEXP ~con~) BEGIN  WRITE_BYTE 0x30 amount  END
      PATCH_IF !(~%stat%~ STRING_CONTAINS_REGEXP ~int~) BEGIN  WRITE_BYTE 0x2a amount  END
      PATCH_IF !(~%stat%~ STRING_CONTAINS_REGEXP ~wis~) BEGIN  WRITE_BYTE 0x2e amount  END
      PATCH_IF !(~%stat%~ STRING_CONTAINS_REGEXP ~cha~) BEGIN  WRITE_SHORT 0x32 amount  END
  END
END

////////////////////////////////////////////////////////////

//pst flags (silver, coldiron, steel, conversable, usable in inventory)
//1 = set, 0 = not set, default is no change
//conversable also needs edit to itemdial.2da (used for item abilities)

DEFINE_ACTION_FUNCTION d2_pstitemflags
  INT_VAR silver=~-1~ coldiron=~-1~ steel=~-1~ conversable=~-1~ inventory=~-1~
  STR_VAR item=~~ resource=~~
BEGIN
  ACTION_FOR_EACH file IN ~%item%~ ~%resource%~ BEGIN
    COPY_EXISTING ~%file%.ITM~  ~override~
      // set flag
      PATCH_IF (silver > 0)      BEGIN  WRITE_LONG 0x18 THIS | BIT8  END
      PATCH_IF (coldiron > 0)    BEGIN  WRITE_LONG 0x18 THIS | BIT9  END
      PATCH_IF (steel > 0)       BEGIN  WRITE_LONG 0x18 THIS | BIT10  END
      PATCH_IF (conversable > 0) BEGIN  WRITE_LONG 0x18 THIS | BIT11  END
      PATCH_IF (inventory > 0)   BEGIN  WRITE_LONG 0x18 THIS | BIT14  END

      // remove flag
      PATCH_IF (silver = 0)      BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT8)  END
      PATCH_IF (coldiron = 0)    BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT9)  END
      PATCH_IF (steel = 0)       BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT10)  END
      PATCH_IF (conversable = 0) BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT11)  END
      PATCH_IF (inventory = 0)   BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT14)  END
    IF_EXISTS BUT_ONLY
  END
END

DEFINE_PATCH_FUNCTION d2_pstitemflags
  INT_VAR silver=~-1~ coldiron=~-1~ steel=~-1~ conversable=~-1~ inventory=~-1~
BEGIN
  PATCH_IF (~%DEST_EXT%~ STR_EQ ~itm~) BEGIN
      // set flag
      PATCH_IF (silver > 0)      BEGIN  WRITE_LONG 0x18 THIS | BIT8  END
      PATCH_IF (coldiron > 0)    BEGIN  WRITE_LONG 0x18 THIS | BIT9  END
      PATCH_IF (steel > 0)       BEGIN  WRITE_LONG 0x18 THIS | BIT10  END
      PATCH_IF (conversable > 0) BEGIN  WRITE_LONG 0x18 THIS | BIT11  END
      PATCH_IF (inventory > 0)   BEGIN  WRITE_LONG 0x18 THIS | BIT14  END

      // remove flag
      PATCH_IF (silver = 0)      BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT8)  END
      PATCH_IF (coldiron = 0)    BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT9)  END
      PATCH_IF (steel = 0)       BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT10)  END
      PATCH_IF (conversable = 0) BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT11)  END
      PATCH_IF (inventory = 0)   BEGIN  WRITE_LONG 0x18 (THIS BAND BNOT BIT14)  END
  END
END

////////////////////////////////////////////////////////////

//remove npc flag from items (for pstee)
//note: npcs may still be restricted to certain types (internally or by class)

DEFINE_ACTION_FUNCTION d2_pstitem_npcflags STR_VAR item=~~ resource=~~ BEGIN
  ACTION_FOR_EACH file IN ~%item%~ ~%resource%~ BEGIN
    COPY_EXISTING ~%file%.ITM~  ~override~
      WRITE_LONG 0x1e (THIS BAND BNOT BIT20)    // Dak'kon
      WRITE_LONG 0x1e (THIS BAND BNOT BIT21)    // Fall-From-Grace
      WRITE_LONG 0x1e (THIS BAND BNOT BIT23)    // Vhailor
      WRITE_LONG 0x1e (THIS BAND BNOT BIT24)    // Ignus
      WRITE_LONG 0x1e (THIS BAND BNOT BIT25)    // Morte
      WRITE_LONG 0x1e (THIS BAND BNOT BIT26)    // Nordom
      WRITE_LONG 0x1e (THIS BAND BNOT BIT28)    // Annah
      WRITE_LONG 0x1e (THIS BAND BNOT BIT30)    // Nameless
    IF_EXISTS BUT_ONLY
  END
END

DEFINE_PATCH_FUNCTION d2_pstitem_npcflags BEGIN
  PATCH_IF (~%DEST_EXT%~ STR_EQ ~itm~) BEGIN
      WRITE_LONG 0x1e (THIS BAND BNOT BIT20)    // Dak'kon
      WRITE_LONG 0x1e (THIS BAND BNOT BIT21)    // Fall-From-Grace
      WRITE_LONG 0x1e (THIS BAND BNOT BIT23)    // Vhailor
      WRITE_LONG 0x1e (THIS BAND BNOT BIT24)    // Ignus
      WRITE_LONG 0x1e (THIS BAND BNOT BIT25)    // Morte
      WRITE_LONG 0x1e (THIS BAND BNOT BIT26)    // Nordom
      WRITE_LONG 0x1e (THIS BAND BNOT BIT28)    // Annah
      WRITE_LONG 0x1e (THIS BAND BNOT BIT30)    // Nameless
  END
END

////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION d2_no_usableflags INT_VAR usable=1 kit1=1 kit2=1 kit3=1 kit4=1 STR_VAR item=~~ resource=~~ BEGIN
  ACTION_FOR_EACH file IN ~%item%~ ~%resource%~ BEGIN
    COPY_EXISTING ~%file%.ITM~  ~override~
      PATCH_IF (usable > 0) BEGIN  WRITE_LONG 0x1e  0  END
      PATCH_IF (kit1 > 0)   BEGIN  WRITE_BYTE 0x29  0  END
      PATCH_IF (kit2 > 0)   BEGIN  WRITE_BYTE 0x2b  0  END
      PATCH_IF (kit3 > 0)   BEGIN  WRITE_BYTE 0x2d  0  END
      PATCH_IF (kit4 > 0)   BEGIN  WRITE_BYTE 0x2f  0  END
    IF_EXISTS BUT_ONLY
  END
END

DEFINE_PATCH_FUNCTION d2_no_usableflags INT_VAR usable=1 kit1=1 kit2=1 kit3=1 kit4=1 BEGIN
  PATCH_IF (~%DEST_EXT%~ STR_EQ ~itm~) BEGIN
      PATCH_IF (usable > 0) BEGIN  WRITE_LONG 0x1e  0  END
      PATCH_IF (kit1 > 0)   BEGIN  WRITE_BYTE 0x29  0  END
      PATCH_IF (kit2 > 0)   BEGIN  WRITE_BYTE 0x2b  0  END
      PATCH_IF (kit3 > 0)   BEGIN  WRITE_BYTE 0x2d  0  END
      PATCH_IF (kit4 > 0)   BEGIN  WRITE_BYTE 0x2f  0  END
  END
END

////////////////////////////////////////////////////////////

//removes all race flags if no variables set (usable by all)
//0 = item usable by race
//1 = item not usable by race

/*if any races are set to 0 or 1 (default is -1)
mode = 0 : other races are not changed
mode = 1 : other races set to 0 (usable)
mode = 2 : other races set to 1 (not usable)
*/

DEFINE_PATCH_FUNCTION d2_item_race_flags
  INT_VAR
    mode      = 0
    human     = ~-1~
    elf       = ~-1~
    halfelf   = ~-1~
    dwarf     = ~-1~
    halfling  = ~-1~
    gnome     = ~-1~
    halforc   = ~-1~
BEGIN
  SET no_flags = 1
  PATCH_IF (human >= 0)    BEGIN SET no_flags = 0 END
  PATCH_IF (elf >= 0)      BEGIN SET no_flags = 0 END
  PATCH_IF (halfelf >= 0)  BEGIN SET no_flags = 0 END
  PATCH_IF (dwarf >= 0)    BEGIN SET no_flags = 0 END
  PATCH_IF (halfling >= 0) BEGIN SET no_flags = 0 END
  PATCH_IF (gnome >= 0)    BEGIN SET no_flags = 0 END
  PATCH_IF (halforc >= 0)  BEGIN SET no_flags = 0 END

  // check "mode" variable
  PATCH_IF (no_flags=0 AND mode=1) BEGIN
    PATCH_IF (human < 0)    BEGIN SET human = 0 END
    PATCH_IF (elf < 0)      BEGIN SET elf = 0 END
    PATCH_IF (halfelf < 0)  BEGIN SET halfelf = 0 END
    PATCH_IF (dwarf < 0)    BEGIN SET dwarf = 0 END
    PATCH_IF (halfling < 0) BEGIN SET halfling = 0 END
    PATCH_IF (gnome < 0)    BEGIN SET gnome = 0 END
    PATCH_IF (halforc < 0)  BEGIN SET halforc = 0 END
  END
  PATCH_IF (no_flags=0 AND mode=2) BEGIN
    PATCH_IF (human < 0)    BEGIN SET human = 1 END
    PATCH_IF (elf < 0)      BEGIN SET elf = 1 END
    PATCH_IF (halfelf < 0)  BEGIN SET halfelf = 1 END
    PATCH_IF (dwarf < 0)    BEGIN SET dwarf = 1 END
    PATCH_IF (halfling < 0) BEGIN SET halfling = 1 END
    PATCH_IF (gnome < 0)    BEGIN SET gnome = 1 END
    PATCH_IF (halforc < 0)  BEGIN SET halforc = 1 END
  END

  // remove restrictions
  PATCH_IF (no_flags>0 OR human=0)    BEGIN WRITE_LONG 0x1e (THIS BAND BNOT BIT27) END
  PATCH_IF (no_flags>0 OR elf=0)      BEGIN WRITE_LONG 0x1e (THIS BAND BNOT BIT23) END
  PATCH_IF (no_flags>0 OR halfelf=0)  BEGIN WRITE_LONG 0x1e (THIS BAND BNOT BIT25) END
  PATCH_IF (no_flags>0 OR dwarf=0)    BEGIN WRITE_LONG 0x1e (THIS BAND BNOT BIT24) END
  PATCH_IF (no_flags>0 OR halfling=0) BEGIN WRITE_LONG 0x1e (THIS BAND BNOT BIT26) END
  PATCH_IF (no_flags>0 OR gnome=0)    BEGIN WRITE_LONG 0x1e (THIS BAND BNOT BIT28) END
  PATCH_IF (no_flags>0 OR halforc=0)  BEGIN WRITE_LONG 0x1e (THIS BAND BNOT BIT31) END

  // add restrictions
  PATCH_IF (human > 0)    BEGIN WRITE_LONG 0x1e THIS | BIT27 END
  PATCH_IF (elf > 0)      BEGIN WRITE_LONG 0x1e THIS | BIT23 END
  PATCH_IF (halfelf > 0)  BEGIN WRITE_LONG 0x1e THIS | BIT25 END
  PATCH_IF (dwarf > 0)    BEGIN WRITE_LONG 0x1e THIS | BIT24 END
  PATCH_IF (halfling > 0) BEGIN WRITE_LONG 0x1e THIS | BIT26 END
  PATCH_IF (gnome > 0)    BEGIN WRITE_LONG 0x1e THIS | BIT28 END
  PATCH_IF (halforc > 0)  BEGIN WRITE_LONG 0x1e THIS | BIT31 END
END

////////////////////////////////////////////////////////////